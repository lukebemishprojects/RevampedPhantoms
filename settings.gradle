pluginManagement {
    repositories {
        mavenCentral()

        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }

        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net'
        }

        maven {
            name = "Luke's Maven"
            url = 'https://maven.lukebemish.dev/releases'
        }

        maven {
            name = 'Architectury'
            url "https://maven.architectury.dev/"
        }

        gradlePluginPortal()
    }
}

plugins {
	id 'dev.lukebemish.conventions' version '0.1.11'
	id 'dev.lukebemish.multisource' version '0.1.8'
}

rootProject.name = 'revampedphantoms'

multisource.of(':') {
	configureEach {
		minecraft.add project.libs.minecraft
		mappings.add loom.layered() {
			officialMojangMappings()
			parchment(project.dependencies.variantOf(project.libs.parchment) {
				artifactType('zip')
			})
		}
	}
	common('main', []) {}
	fabric('fabric', ['main']) {}
	neoforge('neoforge', ['main']) {
		neoForge.add project.libs.neoforge
	}
	repositories {
		// Forge repo doesn't mirror central right...
		it.removeIf { it.name == 'Forge' }
		maven {
			name = 'ParchmentMC'
			url = 'https://maven.parchmentmc.org'
		}
		maven {
			name = "Luke's Maven"
			url = 'https://maven.lukebemish.dev/releases'
		}
	}
}
