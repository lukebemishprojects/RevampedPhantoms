import org.groovymc.modsdotgroovy.core.Platform
import org.groovymc.modsdotgroovy.gradle.tasks.AbstractGatherPlatformDetailsTask

plugins {
	id 'convention.consumer'
	alias libs.plugins.architectury.loom
	alias libs.plugins.mdg
	alias libs.plugins.opensesame
	alias libs.plugins.curseforgegradle
	alias libs.plugins.minotaur
}

opensesame.apply(sourceSets.main)

modsDotGroovy {
	platform = Platform.NEOFORGE
	inferGather.set false
	multiplatform {
		from ':common'
	}
	apply()
}

tasks.named('gatherNeoForgePlatformDetails', AbstractGatherPlatformDetailsTask).configure {
	minecraftVersion = libs.versions.minecraft.get()
	platformVersion = libs.versions.neoforge.get()
}

repositories {
	maven {
		name = 'NeoForged'
		url = 'https://maven.neoforged.net/'
	}
}

configurations {
	localRuntime.extendsFrom localImplementation
	compileOnly.extendsFrom localImplementation
}

dependencies {
	minecraft libs.minecraft
	neoForge libs.neoforge
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${libs.versions.parchment.minecraft.get()}:${libs.versions.parchment.mappings.get()}@zip")
	}

	localImplementation libs.codecextras
	include libs.codecextras

	localImplementation libs.opensesame.core
	include libs.opensesame.core
}

loom {
	runs {
		client {
			client()
			setConfigName("NeoForge Client")
			ideConfigGenerated(true)
			runDir("run")
		}
		server {
			server()
			setConfigName("NeoForge Server")
			ideConfigGenerated(true)
			runDir("runserver")
		}
	}
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper
processResources {
	doLast {
		fileTree(dir: outputs.files.asPath, include: "mixin.*.json").each { File file ->
			def obj = new JsonSlurper().parse(file)
			if (obj.refmap) {
				obj.remove('refmap')
			}
			file.text = JsonOutput.toJson(obj)
		}
	}
}

import net.darkhax.curseforgegradle.TaskPublishCurseForge

if (System.getenv('CURSEFORGE_KEY')) {
	tasks.register('curseforge', TaskPublishCurseForge) {
		disableVersionDetection()
		apiToken = System.getenv('CURSEFORGE_KEY')
		def projectId = '575418'
		def mainFile = upload(projectId, tasks.remapJar)
		mainFile.displayName = "Revamped Phantoms [NeoForge] v$project.version"
		mainFile.releaseType = "${project.release_type}"
		mainFile.addModLoader('NeoForge')
		mainFile.addGameVersion("${libs.versions.minecraft.get()}")
		mainFile.changelog = ''
	}

	afterEvaluate {
		tasks.curseforge.dependsOn tasks.remapJar
	}
}

if (System.getenv('MODRINTH_KEY')) {
	modrinth {
		token = System.getenv('MODRINTH_KEY')
		projectId = 'YJOG99cQ'
		versionNumber = "$project.version"
		versionType = "${project.release_type}"
		detectLoaders = false
		uploadFile = remapJar
		gameVersions = ["${libs.versions.minecraft.get()}"]
		loaders = ['neoforge']
	}
}
