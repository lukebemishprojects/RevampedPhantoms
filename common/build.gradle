plugins {
	id 'convention.shared'
    alias libs.plugins.mdg
	alias libs.plugins.architectury.loom
    alias libs.plugins.opensesame
}

modsDotGroovy {
    multiplatform.expose()
    gather {
        projectProperty 'mod_description'
        projectProperty 'mod_id'
        projectProperty 'mod_name'
    }
    apply()
}

configurations {
	commonJava {
		canBeResolved = false
		canBeConsumed = true
	}
	commonResources {
		canBeResolved = false
		canBeConsumed = true
	}
}

loom {
	runConfigs.configureEach {
		ideConfigGenerated = false
	}
}

import net.fabricmc.loom.task.AbstractRemapJarTask
tasks.withType(AbstractRemapJarTask).each {
	it.targetNamespace = "named"
}

dependencies {
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${libs.versions.parchment.minecraft.get()}:${libs.versions.parchment.mappings.get()}@zip")
	}
	minecraft libs.minecraft
	modCompileOnly libs.fabric.loader

    compileOnly libs.codecextras
    compileOnly libs.opensesame.core
}

artifacts {
	commonJava sourceSets.main.java.sourceDirectories.singleFile
	commonResources sourceSets.main.resources.sourceDirectories.singleFile
}
