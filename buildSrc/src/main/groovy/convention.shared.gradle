plugins {
    id 'java-library'
    id 'maven-publish'
    id 'dev.lukebemish.conventions.java'
    id 'dev.lukebemish.managedversioning'
}

managedVersioning {
    versionFile.set project.rootDir.toPath().resolve('version.properties').toFile()
    gitWorkingDir.set project.rootDir
    metadataVersion.set libs.versions.minecraft
    versionPRs()
    versionSnapshots()
}

managedVersioning.apply()

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
java.withSourcesJar()
java.withJavadocJar()

javadoc {
    include "**/api/**"
}

base {
    archivesName = "${artifact_id}-${project.name}-${libs.versions.minecraft.get()}"
}

repositories {
    mavenCentral()
    maven {
        name = 'Architectury'
        url "https://maven.architectury.dev/"
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }

    // TODO: remove
    maven {
        url "https://maven.lukebemish.dev/snapshots/"
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifactId "$artifact_id-${project.name}"
            from components.java
        }
    }
}

sourcesJar {
    from("${project.rootDir}/LICENSE") {
        rename { "${it}_${artifact_id}" }
    }
}

['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements'].each {
    configurations."$it".outgoing {
        capability("$group:$artifact_id-${project.name}:$version")
    }
}

jar {
    from("${project.rootDir}/LICENSE") {
        rename { "${it}_${artifact_id}" }
    }

    manifest {
        attributes([
            'Specification-Title'       : mod_name,
            'Specification-Vendor'      : mod_author,
            'Specification-Version'     : project.version,
            'Implementation-Title'      : "$mod_name - ${project.name}",
            'Implementation-Version'    : project.version,
            'Implementation-Vendor'     : mod_author,
            'Implementation-Commit-Time': managedVersioning.timestamp.get(),
            'Implementation-Commit'     : managedVersioning.hash.get(),
            'Built-On-Minecraft'        : libs.versions.minecraft.get()
        ])
    }
}
